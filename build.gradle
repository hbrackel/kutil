buildscript {
    ext.kotlin_version = '1.1.60'

    apply from: "${rootDir}/repos.gradle"

    repositories {
        maven { url pluginsRepoUrl }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.2'
    }
}

apply plugin: 'groovy'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'org.junit.platform.gradle.plugin'

group 'de.macnix.util'
version '1.0.2'

repositories {
    maven { url libsRepoUrl }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.7'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.2'

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8"
    compile "org.jetbrains.kotlin:kotlin-reflect"

    // Testing
    testCompile "org.junit.jupiter:junit-jupiter-api:5.0.2"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.0.2"
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    // Kotlin Spek
    testCompile ('org.jetbrains.spek:spek-api:1.1.5') {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntime ('org.jetbrains.spek:spek-junit-platform-engine:1.1.5') {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }



    //testCompile group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.4'
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            pom.withXml {
                asNode().appendNode('licenses')
                        .appendNode('license')
                        .appendNode("name", "Apache License, Version 2.0")
                        .parent().appendNode("url", "https://www.apache.org/licenses/LICENSE-2.0.txt")
                        .parent().appendNode("distribution", "repo")
            }

            from components.java
        }
    }
    repositories {
        maven {
            url libsReleaseLocalRepoUrl
            credentials {
                // in ~/.gradle/gradle.properties
                username deployUser
                password deployPassword
            }
        }
    }
}


